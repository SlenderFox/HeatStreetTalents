import "globalvars.ostw";
import "subroutines.ostw";

rule: "HVT Team 2 Slot 1: Create HVT Entities"
Event.OngoingPlayer
Team.Team2
Player.Slot1
{
	CreateProgressBarHudText(objective_phase_number == 1 || objective_phase_number == 2 ? AllPlayers() : [], objective_phase_number == 1 ? 100 * NormalizedHealth(EventPlayer()) : (downed_start_t ? objective_progress : revive_timer) / 1.5, objective_phase_number == 1 ? "Subdue the High Value Target" : downed_start_t ? ArrayContains(revivers, LocalPlayer()) ? "Capturing..." : "Capture the High Value Target" : "High Value Target is being revived", Location.Top, -97, objective_phase_number == 1 ? CustomColor(Max(200, 255 - (TotalTimeElapsed() - hvt_call_reinforcements_end_t) * 165), Max(0, 255 - (TotalTimeElapsed() - hvt_call_reinforcements_end_t) * 765), Max(19, 255 - (TotalTimeElapsed() - hvt_call_reinforcements_end_t) * 708), 255) : downed_start_t ? downed_start_t == 2 ? Color.Green : Color.Blue : Color.Red, objective_phase_number == 1 ? Color.Blue : downed_start_t ? Color.Blue : Color.Yellow, ProgressBarHudEvaluation.VisibleToValuesAndColor, Spectators.DefaultVisibility);
	CreateIcon(objective_phase_number == 1 || objective_phase_number == 2 ? AllPlayers() : [], EventPlayer(), Icon.ExclamationMark, IconRev.VisibleTo, Color.Yellow, true);
	CreateInWorldText(objective_phase_number == 1 || objective_phase_number == 2 ? AllPlayers() : [], hvt_call_reinforcements_end_t >= TotalTimeElapsed() ? "Calling Reinforcements" : objective_phase_number == 1 ? "Subdue Target" : "Capture Target", EventPlayer(), 1.5, Clipping.DoNotClip, InworldTextRev.VisibleToAndString, Color.Yellow, Spectators.DefaultVisibility);
	CreateEffect(objective_phase_number == 2 ? AllPlayers() : [], Effect.Ring, downed_start_t ? downed_start_t == 2 ? Color.Green : Color.White : Color.Red, PositionOf(EventPlayer()) + 0.65 * Up(), 10, EffectRev.VisibleToPositionRadiusAndColor);
	CreateEffect(objective_phase_number == 2 ? AllPlayers() : [], Effect.Ring, downed_start_t ? downed_start_t == 2 ? Color.Green : Color.White : Color.Red, PositionOf(EventPlayer()) + 0.65 * Down(), 10, EffectRev.VisibleToPositionRadiusAndColor);
	CreateEffect(objective_phase_number == 2 ? AllPlayers() : [], Effect.Ring, downed_start_t ? downed_start_t == 2 ? Color.Green : Color.White : Color.Red, PositionOf(EventPlayer()), (TotalTimeElapsed() % 1.333 < 0.666) * 15 * TotalTimeElapsed() % 10, EffectRev.VisibleToPositionRadiusAndColor);
	CreateEffect(hvt_call_reinforcements_end_t >= TotalTimeElapsed() ? AllPlayers() : [], Effect.LightShaft, Color.White, UpdateEveryFrame(PositionOf(EventPlayer())), 1, EffectRev.VisibleToPositionAndRadius);
}

rule: "HVT Team 2 Slot 1: HVT subdued fallback"
Event.OngoingPlayer
Team.Team2
Player.Slot1
if (objective_phase_number == 2)
if (!HasStatus(EventPlayer(), Status.Stunned))
{
	Wait(0.25, WaitBehavior.AbortWhenFalse);
	AbortIfConditionIsFalse();
	while (objective_phase_number == 2 && !HasStatus(EventPlayer(), Status.Stunned))
	{
		ClearStatus(EventPlayer(), Status.PhasedOut);
		CancelPrimaryAction(EventPlayer());
		SetStatus(EventPlayer(), null, Status.KnockedDown, 9999);
		Wait(0.25, WaitBehavior.IgnoreCondition);
	}
	if (objective_phase_number == 2)
	{
		SetStatus(EventPlayer(), null, Status.PhasedOut, 9999);
	}
	LoopIfConditionIsTrue();
}

rule: "HVT Team 2 Slot 1: Subdued, check Player in Radius"
Event.OngoingPlayer
Team.Team2
Player.Slot1
if (objective_phase_number == 2)
{
	revivers = FilteredArray(combatants, !ArrayElement().downed_start_t && !(ArrayElement().is_invisible % 3) && DistanceBetween(EventPlayer(), Vector(XOf(PositionOf(ArrayElement())), YOf(PositionOf(EventPlayer())), ZOf(PositionOf(ArrayElement())))) <= 10 && AbsoluteValue(YOf(PositionOf(EventPlayer())) - YOf(PositionOf(ArrayElement()))) <= 5 && IsInLineOfSight(EventPlayer(), ArrayElement(), BarrierLOS.NoBarriersBlock));
	if (CountOf(revivers))
	{
		downed_start_t = 2;
		objective_progress += 1.5;
	}
	else if (hvt_dom_objective_killed_count >= 3)
	{
		downed_start_t = 1;
		objective_progress += 0.6;
	}
	else if (downed_start_t > 0)
	{
		downed_start_t -= 0.2;
	}
	else
	{
		revive_timer += hvt_dom_objective_killed_count == 1 ? 3 : 2;
	}
	if (objective_progress >= 150)
	{
		ClearStatus(EventPlayer(), Status.KnockedDown);
		ClearStatus(EventPlayer(), Status.PhasedOut);
		ClearStatus(EventPlayer(), Status.Invincible);
		PlayEffect(AllPlayers(), PlayEffect.GoodPickupEffect, Color.Yellow, PositionOf(EventPlayer()), 1);
		PlayEffect(AllPlayers(), PlayEffect.ExplosionSound, Color.White, PositionOf(EventPlayer()), 200);
		Teleport(EventPlayer(), 1000 * Down());
		start_kill_remaining_phase();
		objective_progress = false;
	}
	else if (revive_timer >= 150)
	{
		objective_phase_number = 1;
		objective_progress = false;
		gmut_fortified_bypass_end_t = false;
		BigMessage(AllPlayers(), "The HVT has been revived!");
		SetPlayerHealth(EventPlayer(), 0.6 * MaxHealth(EventPlayer()));
		ClearStatus(EventPlayer(), Status.KnockedDown);
		ClearStatus(EventPlayer(), Status.PhasedOut);
		ClearStatus(EventPlayer(), Status.Invincible);
	}
	Wait(0.2, WaitBehavior.IgnoreCondition);
	LoopIfConditionIsTrue();
}

rule: "HVT Team 2 Slot 1: Call Reinforcements"
Event.OngoingPlayer
Team.Team2
Player.Slot1
if (objective_phase_number == 1)
if (NormalizedHealth(EventPlayer()) > 0.5)
{
	WaitUntil(NormalizedHealth(EventPlayer()) <= 0.5 || objective_phase_number == 2, 99999);
	if (objective_phase_number == 2)
	{
		enable_normal_spawns = true;
		call_reinforcements();
		async! play_incoming_sound_twice();
	}
	else
	{
		PlayEffect(AllPlayers(), PlayEffect.GoodPickupEffect, Color.Red, EventPlayer(), 1);
		hvt_call_reinforcements_end_t = TotalTimeElapsed() + 10;
		SetDamageReceived(EventPlayer(), 5);
		SetPlayerHealth(EventPlayer(), 0.5 * MaxHealth(EventPlayer()));
		Wait(1, WaitBehavior.IgnoreCondition);
		enable_normal_spawns = true;
		call_reinforcements();
		async! play_incoming_sound_twice();
		Wait(4, WaitBehavior.IgnoreCondition);
		SetDamageReceived(EventPlayer(), 50);
	}
	WaitUntil(objective_phase_number == 3, 99999);
}

rule: "HVT Team 2 Slot 1: Capture Breakpoints"
Event.OngoingPlayer
Team.Team2
Player.Slot1
if (objective_phase_number == 2)
{
	upgrade_health_and_healing();
	money_amount += 105;
	WaitUntil(objective_progress >= 140, 99999);
	enable_normal_spawns = true;
	call_reinforcements();
	async! play_incoming_sound_twice();
	WaitUntil(objective_phase_number == 3, 99999);
	money_amount += 105;
}
