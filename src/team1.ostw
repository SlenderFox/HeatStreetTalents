/** All of team 1's (player team's) functionalities.
*/

import "shared.ostw";

playervar Number downedStartTime;
playervar Number reviveProgress;
playervar Player[] reviverList;
playervar Boolean isChangingHeroes;

void disable_player() playervar "[TEAM 1 SUBROUTINE] Disable Player"
{
	ForceThrottle(EventPlayer(), zero, 0.1, zero, 0.1, zero, 0.1);
	SetPrimaryFireEnabled(EventPlayer(), false);
	SetSecondaryFireEnabled(EventPlayer(), false);
	SetAbility1Enabled(EventPlayer(), false);
	SetAbility2Enabled(EventPlayer(), false);
	SetMeleeEnabled(EventPlayer(), false);
	SetJumpEnabled(EventPlayer(), false);
	SetCrouchEnabled(EventPlayer(), false);
	//DisallowButton(EventPlayer(), Button.Ultimate);
}

void enable_player() playervar "[TEAM 1 SUBROUTINE] Enable Player"
{
	StopForcingThrottle(EventPlayer());
	SetPrimaryFireEnabled(EventPlayer(), true);
	SetSecondaryFireEnabled(EventPlayer(), true);
	SetAbility1Enabled(EventPlayer(), true);
	SetAbility2Enabled(EventPlayer(), true);
	SetMeleeEnabled(EventPlayer(), true);
	SetJumpEnabled(EventPlayer(), true);
	SetCrouchEnabled(EventPlayer(), true);
	//AllowButton(EventPlayer(), Button.Ultimate);
}

rule: "[TEAM 1] Initialise player effects"
Event.OngoingPlayer
Team.Team1
{
	CreateEffect(
		downedStartTime ? AllPlayers() : [],
		Effect.Ring,
		Color.Yellow,
		EventPlayer(),
		3,
		EffectRev.VisibleTo
	);
}

rule: "[TEAM 1] Player downing"
Event.OnDeath
Team.Team1
if (isChangingHeroes == false)
{
	if (!downedStartTime)
	{
		downedStartTime = TotalTimeElapsed();
		disable_player();
		DisableMessages(EventPlayer());
		reviveProgress = zero;
		StartScalingPlayer(EventPlayer(), 0.1, false);
	}
	if (EventWasEnvironment())
	{ RecoveryTeleport(); }
	Resurrect(EventPlayer());
	# Clear any temporary phased out status
	ClearStatus(EventPlayer(), Status.PhasedOut);
	SetStatus(EventPlayer(), null, Status.Invincible, 9999);
	// 0.05
	Wait(0.033, WaitBehavior.RestartWhenTrue);
	SetPlayerHealth(EventPlayer(), one);
	SetStatus(EventPlayer(), null, Status.PhasedOut, 9999);
	DisableMovementCollisionWithPlayers(EventPlayer());
}

void RevivePlayer(in Player pPlayer)
{
	SmallMessage(AllPlayers(), <" <0> has been revived!", pPlayer>);
	pPlayer.downedStartTime = zero;
	EnableMessages(pPlayer);
	ClearStatus(pPlayer, Status.PhasedOut);
	ClearStatus(pPlayer, Status.Invincible);
	EnableMovementCollisionWithPlayers(pPlayer);
	StopScalingPlayer(pPlayer);
}

rule: "[TEAM 1] Revive downed player"
Event.OngoingPlayer
Team.Team1
if (downedStartTime)
if (downedStartTime + one < TotalTimeElapsed())
{
	# Revive area is a cylinder
	reviverList = AllPlayers(Team.Team1).Filter(
		p => !p.downedStartTime
		// Check horizontal distance
		&& DistanceBetween(
			EventPlayer(),
			Vector(
				p.Position().X,
				EventPlayer().Position().Y,
				p.Position().Z
			)
		) <= 3
		// Check vertical distance
		&& AbsoluteValue(EventPlayer().Position().Y - p.Position().Y) <= 3
		// I have no idea what this does, maybe LOS used to not work with ice block?
		//&& (
		//	HeroOf(p) == Hero.Mei
		//	&& IsUsingAbility1(p)
		//	|| IsInLineOfSight(EventPlayer(), p, BarrierLOS.NoBarriersBlock)
		//)
		&& IsInLineOfSight(EventPlayer(), p, BarrierLOS.EnemyBarriersBlock)
	);

	# At least one person is reviving
	if (CountOf(reviverList))
	{
		reviveProgress += reviverList.IsTrueForAll(
			p => HasStatus(p, Status.Invincible)
			|| HasStatus(p, Status.PhasedOut)
		) ? 2.5 : 5;

		if (reviveProgress >= 100)
		{
			enable_player();
			RevivePlayer(EventPlayer());
		}
	}
	else
	{
		reviveProgress = zero;
	}

	Wait(0.2, WaitBehavior.IgnoreCondition);
	LoopIfConditionIsTrue();
}

rule: "[TEAM 1] Changing heroes"
Event.OngoingPlayer
Team.Team1
if (EventPlayer().IsButtonHeld(Button.Reload))
{
	Wait(0.4, WaitBehavior.AbortWhenFalse);
	AbortIf(downedStartTime);
	isChangingHeroes = true;
	# Sets the position as the ground under the player to avoid spawning them midair
	Vector position! = positionUnderPlayer(EventPlayer());
	Vector facing! = EventPlayer().FacingDirection();
	SetAllowedHeroes(EventPlayer(), Hero.Brigitte);
	SetAllowedHeroes(EventPlayer(), Hero.Orisa);
	SetAllowedHeroes(EventPlayer(), AllHeroes());
	Wait(0.1);
	WaitUntil(HasSpawned(EventPlayer()), 9999);
	Teleport(EventPlayer(), position);
	SetFacing(EventPlayer(), facing);
	isChangingHeroes = false;
}
