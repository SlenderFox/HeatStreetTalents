/** Enemy bot management and ai. (Could split)
*/
import "pathfinding/pathfinding.ostw";

// Spawning bots is done from the perspective of the bot, it see how many bots are expected and spawns itself.
// Spawning of bots should be primarily handled by the gamemode.
// Could use the slot of the bot for addressing.

// I have 14 max unique skins.
// I can have 18 dummy bots in total, with 9 unique skins.

// Should the friendly bot be defined and managed here?
globalvar Player friendlyBot;
globalvar Number botQuota;
playervar Player target;
playervar Player following;

void RespawnBot(in Player pDummy)
{
	# Cannot use resurrect as it breaks custom game health settings
	Respawn(pDummy);
	# Minimum time before teleport can function after calling respawn
	Wait(0.033);
	Teleport(pDummy);
}

rule: "[BOTS] Create all the dummy bots" -10
{
	# Create friendly bot
	friendlyBot = CreateDummyBot(
		Hero.Mauga,
		Team.Team1,
		6,
		Vector(0, -1000, 0),
		null
	);
	friendlyBot.pfPlayerAttributes = [zero, 1];
	StartForcingDummyBotName(friendlyBot, "Puipui");

	# Create enemy bots
	for (i = 0; i < 17; i++)
	{
		CreateDummyBot(
			Hero.Soldier76,
			Team.Team2,
			-1,
			Vector(0, -1000, 0),
			null
		).pfPlayerAttributes = [zero, 1, 6, 10, 20];
	}
}

rule: "[BOTS] Following"
Event.OngoingPlayer
if (isDummy)
if (following != null)
if (DistanceBetween(EventPlayer(), following) > 10)
{
	# Pause updates if server is being overloaded
	WaitUntil(ServerLoadAverage() < 230, 999);
	pfDestination = following.Position();
	# Avoid spamming destination updates
	Wait(1);
	LoopIfConditionIsTrue();
}

rule: "[BOTS] Facing"
Event.OngoingPlayer
if (isDummy)
if (target != null)
{
	StartFacing(
		EventPlayer(),
		DirectionTowards(EyePosition(), target.EyePosition()),
		500,
		Relative.ToWorld,
		FacingRev.DirectionAndTurnRate
	);
}

rule: "[BOTS] Target LOS check"
Event.OngoingPlayer
if (isDummy)
if (target != null)
if (
	IsInLineOfSight(
		EyePosition(),
		target.EyePosition(),
		BarrierLOS.NoBarriersBlock
	) == false
)
{
	StopFacing();
	target = null;
}
